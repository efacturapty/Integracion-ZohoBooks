//********** API KEY ***************
// INGRESE AQUÍ EL API KEY DE SU CUENTA DE efacturapty

apiKey = "INGRESE AQUI SU API KEY";

//**********************************
customer = zoho.books.getRecordsByID("Contacts",organization.get("organization_id"),creditnote.get("customer_id"));
resultMap = Map();
resultMap.put("customer",customer);
resultMap.put("creditnote",creditnote);
headers = Map();
headers.put("ApiKeyEfactura",apiKey);
headers.put("Accept-Language","es-PA");
headers.put("Content-Type","application/x-www-form-urlencoded");
autorizacion = invokeurl
[
	url :"https://hub.efacturapty.com/Zoho/CreditMemo"
	type :POST
	parameters:resultMap
	headers:headers
];
autorizacionMap = autorizacion.toMap();
// Se verifica si el retorno de la invocación contiene código exitoso de autorización
if(autorizacion.contains('"autorizada":true'))
{
	// Si es exitoso se actualiza la factura con el cufe, código de autorización y fecha de autorización, son custom fields y empieza la preparación de la estructura del update 
	customFieldList = List();	
	customFieldMap1 = Map();
	customFieldMap1.put('api_name','cf_cufe');
	customFieldMap1.put('value',autorizacionMap.get('cufe'));
	customFieldList.add(customFieldMap1);
	customFieldMap2 = Map();
	customFieldMap2.put('api_name','cf_protocolo_de_autorizaci_n');
	customFieldMap2.put('value',autorizacionMap.get('protocoloAutorizacion'));
	customFieldList.add(customFieldMap2);
	customFieldMap3 = Map();
	customFieldMap3.put('api_name','cf_fecha_de_autorizaci_n');
	customFieldMap3.put('value',autorizacionMap.get('fechaAutorizacion').left(10));
	customFieldList.add(customFieldMap3);
	updateCreditNoteMap = Map();
	updateCreditNoteMap.put('custom_fields',customFieldList);

	// Invocación para la actualización
	updateCreditNote = zoho.books.updateRecord('CreditNotes',organization.get('organization_id'),creditnote.get('creditnote_id'),updateCreditNoteMap);

	// Se inicializa el mensaje de retorno
	resultMap.put("message","Nota de crédito autorizada");
	resultMap.put("code",updateCreditNote);
}
else
{
	// La factura no se autorizó
	resultMap.put("message","Error al autorizar la nota de crédito: " + autorizacionMap.get('rRetEnviFe').get('xProtFe').get('rProtFe').get('gInfProt').get('gResProc'));
	resultMap.put("code",1);
}
resultMap.put("autorizacion",autorizacion);
return resultMap;
