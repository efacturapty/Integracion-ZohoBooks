//********** API KEY ***************
// INGRESE AQUÍ EL API KEY DE SU CUENTA DE efacturapty

apiKey = "INGRESE AQUI SU API KEY";

//**********************************
customer = zoho.books.getRecordsByID("Contacts",organization.get("organization_id"),invoice.get("customer_id"));
resultMap = Map();
resultMap.put("customer",customer);
resultMap.put("invoice",invoice);
headers = Map();
headers.put("ApiKeyEfactura",apiKey);
headers.put("Accept-Language","es-PA");
headers.put("Content-Type","application/x-www-form-urlencoded");
autorizacion = invokeurl
[
	url :"https://hub.efacturapty.com/Zoho/Invoice"
	type :POST
	parameters:resultMap
	headers:headers
];
autorizacionMap = autorizacion.toMap();
// Se verifica si el retorno de la invocación contiene código exitoso de autorización
if(autorizacion.contains('"autorizada":true'))
{
	// Si es exitoso se actualiza la factura con el cufe, código de autorización y fecha de autorización, son custom fields y empieza la preparación de la estructura del update 
	customFieldList = List();
	customFieldMap1 = Map();
	customFieldMap1.put('api_name','cf_cufe');
	customFieldMap1.put('value',autorizacionMap.get('cufe'));
	customFieldList.add(customFieldMap1);
	customFieldMap2 = Map();
	customFieldMap2.put('api_name','cf_protocolo_de_autorizaci_n');
	customFieldMap2.put('value',autorizacionMap.get('protocoloAutorizacion'));
	customFieldList.add(customFieldMap2);
	customFieldMap3 = Map();
	customFieldMap3.put('api_name','cf_fecha_de_autorizaci_n');
	customFieldMap3.put('value',autorizacionMap.get('fechaAutorizacion').left(10));
	customFieldList.add(customFieldMap3);
	updateInvoiceMap = Map();
	updateInvoiceMap.put('custom_fields',customFieldList);
	
	// Invocación para la actualización
	updateInvoice = zoho.books.updateRecord('Invoices',organization.get('organization_id'),invoice.get('invoice_id'),updateInvoiceMap);
	markAsSent = zoho.books.markStatus('Invoices',organization.get('organization_id'),invoice.get('invoice_id'),'sent');
	
	// Se inicializa el mensaje de retorno
	resultMap.put("message","Factura autorizada");
	resultMap.put("code",updateInvoice);
	resultMap.put("autorizacion",autorizacion);
}
else
{
	// La factura no se autorizó
	resultMap.put("message","Error al autorizar la factura: " + autorizacionMap.get('rRetEnviFe').get('xProtFe').get('rProtFe').get('gInfProt').get('gResProc'));
	resultMap.put("code",1);
}

return resultMap;
